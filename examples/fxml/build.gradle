plugins {
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.0.8'
}

application {
  mainClassName = 'com.yworks.example.HelloWorld'
}

compileJava {
  sourceCompatibility '1.8'
  targetCompatibility '1.8'
}

jar {
  manifest {
    attributes(
      'Main-Class': application.mainClassName
    )
  }
}

javafx {
  modules = [ 'javafx.controls', 'javafx.fxml' ]
}

repositories {
  mavenCentral()
}

configurations {
  yguard
}

dependencies {
  yguard 'com.yworks:yguard:4.1.1'
}

task obfuscate {
  dependsOn jar
  group 'yGuard'
  description 'Obfuscates the java archive.'

  doLast {
    def archivePath = jar.archiveFile.get().asFile.path
    def unobfJar = archivePath.replace(".jar", "_unobf.jar")
    def controller = 'com.yworks.example.HelloWorldController'

    ant.move(file: archivePath, tofile: unobfJar, verbose: true)

    ant.taskdef(
      name: 'yguard',
      classname: 'com.yworks.yguard.YGuardTask',
      classpath: configurations.yguard.asPath
    )
    ant.yguard {
      inoutpair(in: unobfJar, out: archivePath)
      rename(logfile: "${buildDir}/${rootProject.name}_renamelog.xml") {
        // Adjust the qualified name of the controller class used in FXML files.
        adjust(replaceContent: true, replaceContentSeparator: ".", includes: "**/*.fxml")
        keep {
          // FXML bindings are based on reflection.
          // While yGuard is able to adjust qualified class names in resource
          // files (like e.g. *.fxml files), it is not able to recognize and
          // adjust member names in such files. Thus, field and method names
          // that are used in *.fxml files have to be excluded from obfuscation.
          field(name: 'clickCountLabel', class: controller)
          method(name: 'void onClicked(javafx.event.ActionEvent)', class: controller)

          method(name: 'void main(java..lang.String[])', class: application.mainClassName)
        }
      }
    }
  }
}

distTar.dependsOn obfuscate
distZip.dependsOn obfuscate
